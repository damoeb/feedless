options {
    STATIC = false;
    IGNORE_CASE = true;
}

PARSER_BEGIN(ComplexArticleFilter)

package org.migor.rich.rss.harvest.entryfilter.complex.generated;

import org.apache.commons.lang3.StringUtils;
import org.migor.rich.rss.database.model.Article;
import java.util.Arrays;
import org.slf4j.Logger;
import org.slf4j.LoggerFactory;

public class ComplexArticleFilter {

  private final Logger log = LoggerFactory.getLogger(ComplexArticleFilter.class);

//  javacc *.jj
//  javac *.java
//  java FilterExprParser < input.txt
//
//  public static void main(String[] args) throws Exception {
//    Scanner scanner = new Scanner(System.in);
//
//    while(scanner.hasNextLine()) {
//      String line = scanner.nextLine();
//      ComplexArticleFilter parser = new ComplexArticleFilter(new ByteArrayInputStream(line.getBytes()));
//      System.out.println(line + " -> " + parser.matches(null));
//    }
//  }

  private String strField(String fieldName, ArticleData data) {
    switch (fieldName) {
      case "content": return data.content;
      case "title": return data.title;
      case "raw": return data.raw;
      case "url": return data.url;
      case "any": return data.content + " " + data.title + " " + data.raw + " " + data.url;
    }
    throw new RuntimeException("Field '" + fieldName + "' is not supported");
  }

  private double numField(String fieldName, ArticleData data) {
    switch (fieldName) {
      case "linkCount": return (double) this.linkCount(data.raw);
//      case "score": return entry.getScore();
    }
    throw new RuntimeException("Field '" + fieldName + "' is not supported");
  }

  private boolean containsInAny(ArticleData data, String search) {
    return StringUtils.containsIgnoreCase(this.strField("any", data), search);
  }

  private int linkCount(String val) {
      return 0;
  }
  private int wordCount(String val) {
    return (int) Arrays.stream(StringUtils.split(val, " ,;:'.!?"))
//      .filter(StringUtils::isNotBlank)
      .count();
  }

  private int sentenceCount(String val) {
    return (int) Arrays.stream(StringUtils.split(val, ";.!?"))
//        .filter(StringUtils::isNotBlank)
        .count();
  }

  public boolean matches(Article article) throws Exception {
    return this.matches(new ArticleData(article.getUrl(), article.getTitle(), article.getContentText(), article.getContentRaw()));
  }

  public boolean matches(String url, String title, String contentText, String raw) throws Exception {
    return this.matches(new ArticleData(url, title, contentText, raw));
  }
}

class ArticleData {
    public final String url;
    public final String title;
    public final String content;
    public final String raw;
    public ArticleData(String url, String title, String content, String raw) {
        this.url = url;
        this.title = title;
        this.content = content;
        this.raw = raw;
    }
}

PARSER_END(ComplexArticleFilter)


SKIP : { " "|"\n"|"\r"|"\r\n" }
TOKEN : { <STR_FIELD : "title" | "content" | "raw" | "url" | "any" > }
TOKEN : { <NUM_FIELD : "score" | "linkCount" > }
TOKEN : { <WORDS : "words" > }
TOKEN : { <LENGTH : "len" > }
TOKEN : { <GT : ">" > }
TOKEN : { <LT :  "<" > }
TOKEN : { <EQ :  "==" > }
TOKEN : { <BOOL : "true" | "false" > }
TOKEN : { <AND : "and" > }
TOKEN : { <OR : "or" > }
TOKEN : { <QUOTE : "'" > }
TOKEN : { <LBRAKE : "(" > }
TOKEN : { <RBRAKE : ")" > }
TOKEN : { <COMMA : "," > }
TOKEN : { <NOT : "not" | "!" > }
TOKEN : { <CONTAINS : "contains" > }
TOKEN : { <ENDSWITH : "endsWith" > }
TOKEN : { <NUMBER : (["0"-"9"])+ > }
TOKEN : { <STRING : (["a"-"z","?"])+ > }

public boolean matches(ArticleData data) throws Exception :
{ boolean b; }
{
    b = CombineBoolExpr(data)
    <EOF>
   { return b; }
}

private boolean CombineBoolExpr(ArticleData data) throws Exception :
{  Token t; boolean boolA, boolB; }
{
      <LBRAKE>
      boolA = CombineBoolExpr(data)
      <RBRAKE>
      {  return boolA;  }
  |
      <NOT>
      <LBRAKE>
      boolA = CombineBoolExpr(data)
      <RBRAKE>
      {  return !boolA;  }
  |

      <AND>
      <LBRAKE>
      boolA = CombineBoolExpr(data)
      <COMMA>
      boolB = CombineBoolExpr(data)
      <RBRAKE>
      {  return boolA && boolB;  }
  |

      <OR>
      <LBRAKE>
      boolA = CombineBoolExpr(data)
      <COMMA>
      boolB = CombineBoolExpr(data)
      <RBRAKE>
      {  return boolA || boolB;  }
  |
      {  return BoolExpr(data);  }
}


private boolean BoolExpr(ArticleData data) throws Exception :
{  Token t; boolean result; boolean boolA, boolB; }
{
  t = <BOOL>
  {  return Boolean.parseBoolean( t.image );  }

  |

  result = StringExpr(data)
  { return result; }

  |

  result = NumberExpr(data)
  { return result; }
}

private boolean StringExpr(ArticleData data) throws Exception :
{  String strA, strB; boolean result; }
{
  <ENDSWITH>
  <LBRAKE>
  strA = StringValue(data)

  <COMMA>
  strB = StringValue(data)
  {  result = StringUtils.endsWithIgnoreCase(strA, strB);  }
  <RBRAKE>
  { return result; }
|
  <CONTAINS>
  <LBRAKE>
  strA = StringValue(data)
  (
    <COMMA>
    strB = StringValue(data)
    {  result = StringUtils.containsIgnoreCase(strA, strB);  }
    <RBRAKE>
    { return result; }

  |

    {  result = containsInAny(data, strA);  }
    <RBRAKE>
    { return result; }

  )

}

private String StringValue(ArticleData data) throws Exception :
{  Token t; }
{
  <QUOTE>
  t = <STRING>
  <QUOTE>
  {  return t.image;  }

  |

  t = <STR_FIELD>
  {  return strField( t.image, data );  }

}


private boolean NumberExpr(ArticleData data) throws Exception :
{  double a, b; String str; }
{
    a = Number(data)
    (
      <GT>
      b = Number(data)
      {  return a > b; }

      |

      <LT>
      b = Number(data)
      {  return a < b; }

      |

      <EQ>
      b = Number(data)
      {  return a == b; }
    )
}


private double Number(ArticleData data) throws Exception :
{  Token t; int i; }
{
  t = <NUMBER>
  {  return Double.parseDouble( t.image );  }

  |

  t = <NUM_FIELD>
  {  return numField( t.image, data );  }

  |

  i = len(data)
  {  return (double)i; }

  |

  i = words(data)
  {  return (double)i; }

}

private int len(ArticleData data) throws Exception :
{  String str; }
{
    <LENGTH>
    <LBRAKE>
    str = StringValue(data)
    <RBRAKE>
    { return str.length(); }

}

private int words(ArticleData data) throws Exception :
{  String str; }
{
    <WORDS>
    <LBRAKE>
    str = StringValue(data)
    <RBRAKE>
    { return wordCount(str); }

}
